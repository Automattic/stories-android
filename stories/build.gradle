plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "org.jetbrains.kotlin.plugin.parcelize"
    id "com.automattic.android.publish-to-s3"
}

apply from: "../stories-common.gradle"

android {
    namespace "com.wordpress.stories"

    compileSdkVersion rootProject.compileSdkVersion


    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'consumer-rules.pro'
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions{
        lintConfig file("${project.rootDir}/lint.xml")
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinxSerializationVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$gradle.ext.kotlinVersion"

    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"

    implementation "androidx.navigation:navigation-fragment-ktx:$androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androidxNavigationVersion"
    implementation "androidx.emoji:emoji:$androidxEmojiVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion"

    implementation "com.google.android.material:material:$googleMaterialVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "jp.wasabeef:glide-transformations:$glideTransformationsVersion"

    implementation "org.greenrobot:eventbus:$eventBusVersion"

    implementation photoEditorProjectDependency

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidxLifecycleVersion"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycleVersion"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"

    implementation "com.github.chrisbanes:PhotoView:$chrisbanesPhotoviewVersion"

    lintChecks "org.wordpress:lint:$wordPressLintVersion"

    testImplementation "junit:junit:$junitVersion"
}

// Add properties named "loop.xxx" to our BuildConfig
android.buildTypes.all { buildType ->
    def inputFile = file("${rootDir}/gradle.properties")
    def properties = loadGradleProperties(inputFile)
    properties.any { property ->
        if (property.key.toLowerCase().startsWith("wp.stories.use.")) {
            buildType.buildConfigField "boolean", property.key.replace("wp.stories.", "").replace(".", "_").toUpperCase(), "${property.value}"
        }
        else if (property.key.toLowerCase().startsWith("wp.stories.")) {
            buildType.buildConfigField "String", property.key.replace("wp.stories.", "").replace(".", "_").toUpperCase(), "\"${property.value}\""
        }
        else if (property.key.toLowerCase().startsWith("wp.stories.res.")) {
            buildType.resValue "string", property.key.replace("wp.stories.res.", "").replace(".", "_").toLowerCase(), "${property.value}"
        }
    }

    if (properties.getProperty("wp.stories.use.cameraX") == null) {
        // use cameraX implementation by default if no gradle.properties set
        buildType.buildConfigField "boolean", "USE_CAMERAX", "true"
    }
}

project.afterEvaluate {
    publishing {
        publications {
            StoriesPublication(MavenPublication) {
                from components.release

                groupId "com.automattic"
                artifactId "stories"
                artifact tasks.named("androidSourcesJar") // This task is added by 'publish-to-s3' plugin
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}
