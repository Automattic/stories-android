apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.sentry.android.gradle'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.automattic.loop"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 12
        // Allow versionName to be overridden with property. e.g. -PversionName=1234
        if (project.hasProperty("versionName")) {
            versionName project.property("versionName")
        } else {
            versionName "1.0.0.14-prealpha"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }

    useLibrary 'android.test.runner'
}

ext {
    flipperVersion = '0.50.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navComponentVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navComponentVersion"
    implementation 'androidx.emoji:emoji:1.0.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.preference:preference-ktx:1.1.1"

    implementation 'com.google.android.material:material:1.2.1'

    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'io.sentry:sentry-android:1.7.27'
    // this dependency is not required if you are already using your own
    // slf4j implementation
    implementation 'org.slf4j:slf4j-nop:1.7.25'

    implementation project(path: ':photoeditor')
    implementation project(path: ':stories')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    implementation('org.wordpress:utils:1.26')

    debugImplementation "com.facebook.flipper:flipper:$flipperVersion"
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'
    debugImplementation "com.facebook.flipper:flipper-network-plugin:$flipperVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

tasks.register('createBuildConfigFieldsFromProperties') {
    // Add properties named "loop.xxx" to our BuildConfig
    android.buildTypes.all { buildType ->
        def properties = rootProject.loadGradleProperties()
        properties.any { property ->
        if (property.key.toLowerCase().startsWith("loop.")) {
                buildType.buildConfigField "String", property.key.replace("loop.", "").replace(".", "_").toUpperCase(),
                        "\"${property.value}\""
            }
            else if (property.key.toLowerCase().startsWith("sentry.dsn")) {
                buildType.buildConfigField "String", property.key.replace(".", "_").toUpperCase(),
                        "\"${property.value}\""
            }
            else if (property.key.toLowerCase().startsWith("loop.res.")) {
                buildType.resValue "string", property.key.replace("loop.res.", "").replace(".", "_").toLowerCase(),
                        "${property.value}"
            }
        }
    }

    // For app signing
    if (["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0) {
        android {
            signingConfigs {
                release {
                    storeFile = rootProject.file(project.storeFile)
                    storePassword = project.storePassword
                    keyAlias = project.keyAlias
                    keyPassword = project.keyPassword
                }
            }
        }
        android.buildTypes.release.signingConfig = android.signingConfigs.release
    }
}

preBuild.dependsOn(tasks.named("createBuildConfigFieldsFromProperties"))
