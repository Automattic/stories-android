plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "io.sentry.android.gradle"
    id "org.jetbrains.kotlin.kapt"
}

apply from: "../stories-common.gradle"

android {
    namespace "com.automattic.loop"

    compileSdkVersion rootProject.compileSdkVersion

    signingConfigs {
        release {
            if (rootProject.hasProperty("storeFile")
                && rootProject.hasProperty("storePassword")
                && rootProject.hasProperty("keyAlias")
                && rootProject.hasProperty("keyPassword")
            ) {
                storeFile file("${rootDir}/${rootProject.storeFile}")
                storePassword rootProject.storePassword
                keyAlias rootProject.keyAlias
                keyPassword rootProject.keyPassword
            }
        }
    }

    defaultConfig {
        applicationId "com.automattic.loop"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 12
        // Allow versionName to be overridden with property. e.g. -PversionName=1234
        if (project.hasProperty("versionName")) {
            versionName project.property("versionName")
        } else {
            versionName "1.0.0.14-prealpha"
        }
        signingConfig signingConfigs.release
    }
    viewBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        lintConfig file("${project.rootDir}/lint.xml")
        disable("IconLocation")
    }
    buildFeatures {
        buildConfig true
    }

    useLibrary 'android.test.runner'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androidxNavigationVersion"
    implementation "androidx.emoji:emoji:$androidxEmojiVersion"
    implementation "androidx.preference:preference-ktx:$androidxPreferenceVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion"

    implementation "com.google.android.material:material:$googleMaterialVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation "org.greenrobot:eventbus:$eventBusVersion"

    implementation "io.sentry:sentry-android:$sentryAndroidVersion"
    // this dependency is not required if you are already using your own
    // slf4j implementation
    implementation "org.slf4j:slf4j-nop:$slf4jVersion"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidxLifecycleVersion"

    implementation project(path: ':photoeditor')
    implementation project(path: ':stories')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"

    implementation('org.wordpress:utils:trunk-1ed207c03d2242b6fc3d74f9e388e9163cbc82a6')

    debugImplementation "com.facebook.flipper:flipper:$flipperVersion"
    debugImplementation "com.facebook.soloader:soloader:$soloaderVersion"
    debugImplementation "com.facebook.flipper:flipper-network-plugin:$flipperVersion"
}

// Add properties named "loop.xxx" to our BuildConfig
android.buildTypes.all { buildType ->
    def inputFile = file("${rootDir}/gradle.properties")
    def properties = loadGradleProperties(inputFile)
    properties.any { property ->
    if (property.key.toLowerCase().startsWith("loop.")) {
            buildType.buildConfigField "String", property.key.replace("loop.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
        else if (property.key.toLowerCase().startsWith("sentry.dsn")) {
            buildType.buildConfigField "String", property.key.replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
        else if (property.key.toLowerCase().startsWith("loop.res.")) {
            buildType.resValue "string", property.key.replace("loop.res.", "").replace(".", "_").toLowerCase(),
                    "${property.value}"
        }
    }
}

